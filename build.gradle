plugins {
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id 'java'
    id 'checkstyle'
}

group = 'edu.kit.provideq'
version = '0.4.0'
sourceCompatibility = '17'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.0.4'
    implementation 'com.bpodgursky:jbool_expressions:1.24'
    implementation 'com.github.ProvideQ.jplex:input:703b4d0086'
    implementation files('lib/de.ovgu.featureide.lib.fm-v3.9.1.jar', 'lib/uvl-parser.jar')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:5.+'
}

jar {
    // avoid creation of plain jar, spring boot will create a fat jar anyways
    enabled = false
}

// inject JUnit into test task
tasks.named('test') {
    useJUnitPlatform()
}

// set Java language level
targetCompatibility = JavaVersion.VERSION_17

tasks.withType(JavaCompile).configureEach {
    // treat "unchecked" warnings as errors
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
    options.compilerArgs << '-Werror'
}

checkstyle {
    toolVersion = '10.12.0'
}
